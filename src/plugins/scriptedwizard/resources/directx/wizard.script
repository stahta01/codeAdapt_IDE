////////////////////////////////////////////////////////////////////////////////
//
// Direct/X project wizard
//
////////////////////////////////////////////////////////////////////////////////

// globals
DirectXPath <- wxT_2("");
DirectXSel <- 1;  // To hold selection
DirectXVerStr <- wxT_2("9");
DirectXVer  <- 9;

function BeginWizard()
{
    local intro_msg = wxT_2("Welcome to the new Direct/X project wizard!\n\n" +
                         "This wizard will guide you to create a new project\n" +
                         "using the Direct/X extensions.\n\n" +
                         "This wizard expects the global variable \"dx\" to point\n" +
                         "to the appropriate DirectX SDK.\n" +
                         "This is the MS DirectX SDK for a Visual C++ Toolkit 2003 project\n" +
                         "and e.g. the DevPack folder for a GCC project.\n\n" +
                         "For a Visual C++ Toolkit 2003 project in addition the global variable\n" +
                         "\"psdk\" must point to the directory of the MS platform SDK.\n\n" +
                         "You will be asked to setup the variables accordingly. If this is\n" +
                         "not the case, verify \"Settings->Global variables\"\n" +
                         "after this wizard has completed.\n\n" +
                         "When you're ready to proceed, please click \"Next\"...");

    local dxpath_descr = wxT_2("Please select the location of the Direct/X SDK on your computer.\n" +
                            "This is the top-level folder where Direct/X was installed (unpacked).\n" +
                            "To help you, this folder must contain the subfolders\n" +
                            "\"include\" and \"lib\".");

    Wizard.AddInfoPage(wxT_2("DirectXIntro"), intro_msg);
    Wizard.AddGenericSingleChoiceListPage(wxT_2("DirectXVersionPage"), wxT_2("Please select the DirectX version you want to use."), wxT_2("DirectX 8;DirectX 9"), DirectXSel); // select wxwidgets version
    Wizard.AddProjectPathPage();
    Wizard.AddCompilerPage(wxT_2(""), wxT_2("gcc*;msvc*"), true, true);
    Wizard.AddGenericSelectPathPage(wxT_2("DirectXPath"), dxpath_descr, wxT_2("Please select Direct/X's location:"), wxT_2("$(#dx)"));
}

////////////////////////////////////////////////////////////////////////////////
// Direct/X's path page
////////////////////////////////////////////////////////////////////////////////

function OnEnter_DirectXVersionPage(fwd)
{
    Wizard.SetListboxSelection(wxT_2("GenericChoiceList"), DirectXSel);
    return true;
}

function OnLeave_DirectXVersionPage(fwd)
{
    if (fwd)
    {
        DirectXSel = Wizard.GetListboxSelection(wxT_2("GenericChoiceList"));
        switch (DirectXSel)
        {
            case 0:
                DirectXVer = 8;
                DirectXVerStr = wxT_2("8");
                break;
            case 1:
                DirectXVer = 9;
                DirectXVerStr = wxT_2("9");
                break;
            default:
                DirectXVer = 9;
                DirectXVerStr = wxT_2("9");
                break;
        }
    }
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// Direct/X's path page
////////////////////////////////////////////////////////////////////////////////

function OnLeave_DirectXPath(fwd)
{
    if (fwd)
    {
        local dir            = Wizard.GetTextControlValue(wxT_2("txtFolder")); // txtFolder is the text control in GenericSelectPathPage
        local dir_nomacro    = ReplaceMacros(dir, true);
        local directX_header = wxT_2("/include/d3d") + DirectXVerStr + wxT_2(".h");
        if (!IO.FileExists(dir_nomacro + directX_header))
        {
            ShowError(wxT_2("The path you entered seems valid, but this wizard " +
                         "can't locate Direct/X's files in it..."));
            return false;
        }

        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc*")))
        {
            local psdk = ReplaceMacros(wxT_2("$(#psdk)"), true);
            if (!IO.DirectoryExists(psdk))
            {
                ShowError(wxT_2("The path to the platform SDK seems not to be valid.\n" +
                             "This setup requires the MS platform SDK..."));
                return false;
            }
        }

        DirectXPath = dir;
    }
    return true;
}

// return the files this project contains
function GetFilesDir()
{
    if ( DirectXVer==8 )
        return wxT_2("directx/dx8");

    return wxT_2("directx/dx9");
}

// setup the already created project
function SetupProject(project)
{
    // set project options
    local gvar = ReplaceMacros(wxT_2("$(#dx)"), true);

    // set compiler/linker search paths
    // see if the Direct/X SDK matches the global var. if it does, use the var instead...
    if (gvar.Matches(DirectXPath))
    {
        project.AddIncludeDir(wxT_2("$(#dx.include)"));
        project.AddLibDir(wxT_2("$(#dx.lib)"));
    }
    else
    {
        project.AddIncludeDir(DirectXPath + wxT_2("/include"));
        project.AddLibDir(DirectXPath + wxT_2("/lib"));
        project.AddLibDir(DirectXPath + wxT_2("/lib/x86"));
    }

    // set compiler options
    project.AddCompilerOption(wxT_2("-DWIN32"));
    project.AddCompilerOption(wxT_2("-DNDEBUG"));
    project.AddCompilerOption(wxT_2("-D_WINDOWS"));
    project.AddCompilerOption(wxT_2("-D_MBCS"));

    // set linker options
    project.AddLinkLib(wxT_2("kernel32"));
    project.AddLinkLib(wxT_2("user32"));
    project.AddLinkLib(wxT_2("gdi32"));
    project.AddLinkLib(wxT_2("winspool"));
    project.AddLinkLib(wxT_2("comdlg32"));
    project.AddLinkLib(wxT_2("advapi32"));
    project.AddLinkLib(wxT_2("shell32"));
    project.AddLinkLib(wxT_2("ole32"));
    project.AddLinkLib(wxT_2("oleaut32"));
    project.AddLinkLib(wxT_2("uuid"));
    project.AddLinkLib(wxT_2("odbc32"));
    project.AddLinkLib(wxT_2("odbccp32"));
    if ( DirectXVer==8 )
    {
        project.AddLinkLib(wxT_2("d3d8"));
    }
    else
    {
        project.AddLinkLib(wxT_2("d3d9"));
    }

    // set additional path's for MS VC++ Toolkit
    if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc*")))
    {
        // set project options for MS Visual C++ Toolkit
        project.AddIncludeDir(wxT_2("$(#psdk.include)"));
        project.AddLibDir(wxT_2("$(#psdk.lib)"));
    }

    // enable compiler warnings (project-wide)
    WarningsOn(project, Wizard.GetCompilerID());

    // Debug
    local target = project.GetBuildTarget(Wizard.GetDebugName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttConsoleOnly); // ttConsoleOnly: console for debugging
        target.SetOutputFilename(Wizard.GetDebugOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        target.SetWorkingDir(Wizard.GetDebugOutputDir());
        // enable generation of debugging symbols for target
        DebugSymbolsOn(target, Wizard.GetCompilerID());
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc")))
        {
            target.AddCompilerOption(wxT_2("-D_DEBUG"));
            target.AddCompilerOption(wxT_2("-ggdb")); // Build for GDB
            if ( DirectXVer==8 )
                target.AddLinkLib(wxT_2("d3dx8"));
            else
                target.AddLinkLib(wxT_2("d3dx9"));
        }
    }

    // Release
    target = project.GetBuildTarget(Wizard.GetReleaseName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttExecutable); // ttExecutable: no console
        target.SetOutputFilename(Wizard.GetReleaseOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        target.SetWorkingDir(Wizard.GetReleaseOutputDir());
        // enable optimizations for target
        OptimizationsOn(target, Wizard.GetCompilerID());
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc")))
        {
            if ( DirectXVer==8 )
                target.AddLinkLib(wxT_2("d3dx8"));
            else
                target.AddLinkLib(wxT_2("d3dx9"));
        }
    }

    return true;
}
