////////////////////////////////////////////////////////////////////////////////
//
// wxWidgets project wizard
//
////////////////////////////////////////////////////////////////////////////////

// globals
WizType <- wizProject;
WxPath <- wxT_2("");
WantPCH <- false;
IsDLL <- false;
IsMonolithic <- false;
IsUnicode <- false;
IsAdvOpt <- false;
IsEmpty <- false; // For empty projects
IsPartialDebug <- false; // For debug linking against release libraries
Configuration <- wxT_2("");
LibPath <- wxT_2("");
LibPrefix <- wxT_2(""); // Prefix of lib name
LibWxVer <- wxT_2(""); // Determines wx version
LibUnicSuffix <- wxT_2(""); // Suffix for Unicode
LibDebugSuffix <- wxT_2("d"); // Suffix for Debug wx Lib
LibSuffix <- wxT_2(""); // Suffix for Lib, defines file extension
LibWxJpeg <- false; // JPEG Lib
LibWxTiff <- false; //TIFF Lib
LibWxRegex <- false; // Regex Lib
LibWxExpat <- false; // Expat Lib
LibWxXml <- false; // XML Lib
LibWxXrc <- false; // XRC Lib
LibWxAdv <- false; //Adv Lib
LibWxAdvUI <- false; // Adv UI Lib
LibWxHtml <- false; // HTML Lib
LibWxOdbc <- false; //ODBC Lib
LibWxMedia <- false; // Media Lib
LibWxNet <- false; // Net Lib
LibWxOpengl <- false; // OpenGL Lib
LibWxQa <- false; // QA Lib
LibWxRichText <- false; // Richtext Lib
WxVersion <- 1; // 0 - wx 2.6, 1 - wx 2.8, 2 - wx 3.0
DebugTarget <- 1; // Target Type; 0 - Console, 1 - GUI
ReleaseTarget <- 1; // Target Type; 0 - Console, 1 - GUI
FileName <- wxT_2(""); // Filename for wizard
ProjAuthor <- wxT_2(""); // Project Author
ProjEmail <- wxT_2(""); // Author's e-mail
ProjWebsite <- wxT_2(""); // Project website
PCHFileName <- wxT_2("wx_pch.h"); // PCH filename
ChoiceWxUnixLib <- 0; // wx lib choice in Unix; 0 for default, 1 for advanced
ChkWxDebug <- true; // Adds wxdebug and debug wx lib. By default it's set to True
GuiBuilder <- 0; // Default to None. 0-None, 1-wxSmith, 2-wxFormBuilder
GuiAppType <- 1; // Default to Dialog. 0-Dialog, 1-Frame
AddlLibList <- wxT_2(""); // Contains the complete list
multi_thread_dynamic <- true; //Default to Multi-thread. For MSVC only.


function BeginWizard()
{
    WizType = Wizard.GetWizardType();

    if (WizType == wizProject)
    {
        local intro_msg = wxT_2("Welcome to the new wxWidgets 2.6.x / 2.8.x / 3.0.x\nproject wizard!\n\n" +
                            "This wizard will guide you to create a new project using\n" +
                            "the wxWidgets cross-platform GUI library.\n\n" +
                            "When you 're ready to proceed, please click \"Next\"...");

        local wxpath_msg = wxT_2("Please select the location of wxWidgets on your computer.\n" +
                                "This is the top-level folder where wxWidgets was unpacked.\n" +
                                "To help you, this folder must contain the subfolders\n" +
                                "\"include\" and \"lib\".");

        Wizard.AddInfoPage(wxT_2("WxIntro"), intro_msg);
        Wizard.AddGenericSingleChoiceListPage(wxT_2("wxVersionPage"), wxT_2("Please select the wxWidgets version you want to use."), wxT_2("wxWidgets 2.6.x;wxWidgets 2.8.x;wxWidgets 3.0.x"), WxVersion); // select wxwidgets version
        Wizard.AddProjectPathPage();
        Wizard.AddPage(wxT_2("WxProjDetails"));
        Wizard.AddPage(wxT_2("WxGuiSelect"));
        if (PLATFORM == PLATFORM_MSW)
            Wizard.AddGenericSelectPathPage(wxT_2("WxPath"), wxpath_msg, wxT_2("wxWidgets' location:"), wxT_2("$(#wx)"));
        // we need the compiler selection before wx settings, because we 'll have
        // to validate the settings. To do this we must know the compiler beforehand...
        Wizard.AddCompilerPage(wxT_2(""), wxT_2("*"), true, true);
        if (PLATFORM == PLATFORM_MSW)
            Wizard.AddPage(wxT_2("WxConf")); // only for windows
        else
            Wizard.AddPage(wxT_2("WxConfUnix")); // just PCH option
        if (PLATFORM == PLATFORM_MSW)
        {
            Wizard.AddPage(wxT_2("WxConfAdvOpt")); // Wizard page to select target type
            Wizard.AddPage(wxT_2("WxAddLib")); // Add additional wx libraries
        }
    }
    else if (WizType == wizTarget)
    {
        local intro_msg = wxT_2("Welcome to the new wxWidgets 2.6.x / 2.8.x / 3.0.x\nTarget wizard!\n\n" +
                            "This wizard will guide you to create a new target\n" +
                            "When you 're ready to proceed, please click \"Next\"...");

        local wxpath_msg = wxT_2("Please select the location of wxWidgets on your computer.\n" +
                                "This is the top-level folder where wxWidgets was unpacked.\n" +
                                "To help you, this folder must contain the subfolders\n" +
                                "\"include\" and \"lib\".");

        Wizard.AddInfoPage(wxT_2("WxIntro"), intro_msg);
        Wizard.AddGenericSingleChoiceListPage(wxT_2("wxVersionPage"), wxT_2("Please select the wxWidgets version you want to use."), wxT_2("wxWidgets 2.6.x;wxWidgets 2.8.x;wxWidgets 3.0.x"), WxVersion); // select wxwidgets version
        if (PLATFORM == PLATFORM_MSW)
            Wizard.AddGenericSelectPathPage(wxT_2("WxPath"), wxpath_msg, wxT_2("wxWidgets' location:"), wxT_2("$(#wx)"));
        // we need the compiler selection before wx settings, because we 'll have
        // to validate the settings. To do this we must know the compiler beforehand...
        Wizard.AddBuildTargetPage(wxT_2(""), false, true, wxT_2(""), wxT_2("*"), true);
        if (PLATFORM == PLATFORM_MSW)
            Wizard.AddPage(wxT_2("WxConf")); // only for windows
        else
            Wizard.AddPage(wxT_2("WxConfUnix")); // just PCH option
        if (PLATFORM == PLATFORM_MSW)
        {
            Wizard.AddPage(wxT_2("WxConfAdvOpt")); // Wizard page to select target type
            Wizard.AddPage(wxT_2("WxAddLib")); // Add additional wx libraries
        }
    }
}

////////////////////////////////////////////////////////////////////////////////
// wxWidgets' version page
////////////////////////////////////////////////////////////////////////////////

function OnEnter_wxVersionPage(fwd)
{
    if (fwd)
    {
        WxVersion = Wizard.GetListboxSelection(wxT_2("GenericChoiceList"));
    }
    return true;
}

function OnLeave_wxVersionPage(fwd)
{
    if (fwd)
    {
        WxVersion = Wizard.GetListboxSelection(wxT_2("GenericChoiceList"));
    }
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// Project Details
////////////////////////////////////////////////////////////////////////////////

function OnEnter_WxProjDetails(fwd)
{
    if (fwd)
    {
        Wizard.SetTextControlValue(wxT_2("txtProjAuthor"), GetConfigManager().Read(wxT_2("/wx_project_wizard/author"), wxT_2("")));
        Wizard.SetTextControlValue(wxT_2("txtProjEmail"), GetConfigManager().Read(wxT_2("/wx_project_wizard/email"), wxT_2("")));
        Wizard.SetTextControlValue(wxT_2("txtProjWebsite"), GetConfigManager().Read(wxT_2("/wx_project_wizard/website"), wxT_2("")));
    }
    return true;
}

function OnLeave_WxProjDetails(fwd)
{
    if (fwd)
    {
        ProjAuthor = Wizard.GetTextControlValue(wxT_2("txtProjAuthor"));
        ProjEmail = Wizard.GetTextControlValue(wxT_2("txtProjEmail"));
        ProjWebsite = Wizard.GetTextControlValue(wxT_2("txtProjWebsite"));
    }
    GetConfigManager().Write(wxT_2("/wx_project_wizard/author"), ProjAuthor);
    GetConfigManager().Write(wxT_2("/wx_project_wizard/email"), ProjEmail);
    GetConfigManager().Write(wxT_2("/wx_project_wizard/website"), ProjWebsite);
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// Project GUI Builder Details
////////////////////////////////////////////////////////////////////////////////

function OnEnter_WxGuiSelect(fwd)
{
    if (fwd)
    {
        GuiBuilder = ConfigManager.Read(wxT_2("/wx_project_wizard/guibuilder"), 0);
        GuiAppType = ConfigManager.Read(wxT_2("/wx_project_wizard/guiapptype"), 0);
        Wizard.SetRadioboxSelection(wxT_2("RB_GUISelect"), GuiBuilder);
        Wizard.SetRadioboxSelection(wxT_2("RB_GUIAppType"), GuiAppType);
    }
    return true;
}

function OnLeave_WxGuiSelect(fwd)
{
    if (fwd)
    {
        GuiBuilder = Wizard.GetRadioboxSelection(wxT_2("RB_GUISelect"));
        GuiAppType = Wizard.GetRadioboxSelection(wxT_2("RB_GUIAppType"));
        if ( GuiBuilder==1 )
        {
            if ( !("WxsAddWxExtensions" in getroottable()) )
            {
                ShowInfo(wxT_2("wxSmith plugin is not loaded, can not continue"));
                return false;
            }
        }
        GetConfigManager().Write(wxT_2("/wx_project_wizard/guibuilder"), GuiBuilder);
        GetConfigManager().Write(wxT_2("/wx_project_wizard/guiapptype"), GuiAppType);
    }
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// wxWidgets' path page
////////////////////////////////////////////////////////////////////////////////

function OnLeave_WxPath(fwd)
{
    if (fwd)
    {
        local dir         = Wizard.GetTextControlValue(wxT_2("txtFolder"));
        local dir_nomacro = ReplaceMacros(dir, true);
        if (!IO.FileExists(dir_nomacro + wxT_2("/include/wx/wx.h")))
        {
            ShowError(wxT_2("The path you entered seems valid, but this wizard " +
                    "can't locate wxWidgets' files in it..."));
            return false;
        }

        // see if it matches the global var. if it does, use the var instead...
        if (GetUserVariableManager().Exists(wxT_2("#wx")))
        {
            local gvar = ReplaceMacros(wxT_2("$(#wx)"), true);
            if (gvar.Matches(dir_nomacro))
                dir = gvar;
        }
        WxPath = dir;
    }
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// wxWidgets' settings
////////////////////////////////////////////////////////////////////////////////

function OnEnter_WxConf(fwd)
{
    if (fwd)
    {
        Wizard.CheckCheckbox(wxT_2("chkWxConfDLL"), IntToBool(ConfigManager.Read(wxT_2("/wx_project_wizard/dll"), 0)));
        Wizard.CheckCheckbox(wxT_2("chkWxConfMono"), IntToBool(ConfigManager.Read(wxT_2("/wx_project_wizard/monolithic"), 0)));
        Wizard.CheckCheckbox(wxT_2("chkWxConfUni"), IntToBool(ConfigManager.Read(wxT_2("/wx_project_wizard/unicode"), 0)));
        Wizard.CheckCheckbox(wxT_2("chkWxConfAdvOpt"), IntToBool(ConfigManager.Read(wxT_2("/wx_project_wizard/debug"), 0)));
        Wizard.CheckCheckbox(wxT_2("chkWxConfPCH"), IntToBool(ConfigManager.Read(wxT_2("/wx_project_wizard/pch"), 0)));
        Wizard.SetTextControlValue(wxT_2("txtWxConfConfig"), ConfigManager.Read(wxT_2("/wx_project_wizard/configuration"), wxT_2("")));
    }
    return true;
}

function OnLeave_WxConf(fwd)
{
    if (fwd)
    {
        IsDLL = Wizard.IsCheckboxChecked(wxT_2("chkWxConfDLL"));
        IsMonolithic = Wizard.IsCheckboxChecked(wxT_2("chkWxConfMono"));
        IsUnicode = Wizard.IsCheckboxChecked(wxT_2("chkWxConfUni"));
        IsEmpty = Wizard.IsCheckboxChecked(wxT_2("chkWxEmpty"));
        IsAdvOpt = Wizard.IsCheckboxChecked(wxT_2("chkWxConfAdvOpt"));
        WantPCH = Wizard.IsCheckboxChecked(wxT_2("chkWxConfPCH"));
        Configuration = Wizard.GetTextControlValue(wxT_2("txtWxConfConfig"));

        // Ask the user whether wizard shall add PCH support when empty Project is selected
        if (IsEmpty && WantPCH)
        {
            local msg = wxT_2("You have selected PCH support for Empty project.\n\n");
            msg = msg + wxT_2("Wizard will add support for PCH assuming the PCH header name as wx_pch.h\n\n");
            msg = msg + wxT_2("Click Yes to accept default settings\n\nClick No to Enter PCH header manually");
            local return_val = Message(msg, wxT_2("wxWidgets Project Wizard"), wxICON_QUESTION | wxYES_NO);
            if (return_val == wxID_NO)
            {
                msg = wxT_2("Please enter PCH header file name");
                PCHFileName = wxGetTextFromUser(msg, wxT_2("wxWidgets Wizard"), wxT_2("wxprec.h"));
                if (PCHFileName.IsEmpty()) // Check for empty string
                    PCHFileName = wxT_2("wx_pch.h");
            }
            else if (return_val == wxID_YES)
                PCHFileName = wxT_2("wx_pch.h");
        }
        else // Set PCHFileName to Default
            PCHFileName = wxT_2("wx_pch.h");

        // Now write the configurations
        ConfigManager.Write(wxT_2("/wx_project_wizard/dll"), BoolToInt(IsDLL));
        ConfigManager.Write(wxT_2("/wx_project_wizard/monolithic"), BoolToInt(IsMonolithic));
        ConfigManager.Write(wxT_2("/wx_project_wizard/unicode"), BoolToInt(IsUnicode));
        ConfigManager.Write(wxT_2("/wx_project_wizard/debug"), BoolToInt(IsAdvOpt));
        ConfigManager.Write(wxT_2("/wx_project_wizard/pch"), BoolToInt(WantPCH));

        // validate settings
        local lib_prefix;
        local lib_wxver;
        local lib_unic_suffix;
        local lib_suffix;
        local lib = WxPath + wxT_2("/lib/");
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc*")))
        {
            lib = lib + wxT_2("gcc_");
            lib_prefix = wxT_2("lib");
            lib_suffix = wxT_2(".a");
        }
        else if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc*")))
        {
            lib = lib + wxT_2("vc_");
            lib_prefix = wxT_2("");
            lib_suffix = wxT_2(".lib");
        }
        else if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("bcc*")))
        {
            lib = lib + wxT_2("bcc_");
            lib_prefix = wxT_2("");
            lib_suffix = wxT_2(".lib");
        }
        else if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("ow")))
        {
            lib = lib + wxT_2("wat_");
            lib_prefix = wxT_2("");
            lib_suffix = wxT_2(".lib");
        }


        if (IsDLL)
            lib = lib + wxT_2("dll");
        else
            lib = lib + wxT_2("lib");

        lib = lib + Configuration;

        // at this point we have the full path to the link libraries
        LibPath = lib;

        lib = lib + wxT_2("/");

        local lib_name = lib_prefix;

        if (IsUnicode)
            lib_unic_suffix = wxT_2("u");
        else
            lib_unic_suffix = wxT_2("");

        if (WxVersion == 0)
            lib_wxver = wxT_2("26");
        else if (WxVersion == 1)
            lib_wxver = wxT_2("28");
        else if (WxVersion == 2)
            lib_wxver = wxT_2("30");

        // Now set the global variables
        LibPrefix = lib_prefix; // Prefix of lib name
        LibWxVer <- lib_wxver; // Determines wx version
        LibUnicSuffix <- lib_unic_suffix; // Suffix for Unicode
        LibSuffix <- lib_suffix; // Suffix for Lib, defines file extension

        // we can finally check for existence :)
        local lib_deb_name = wxT_2("");
        local lib_rel_name = wxT_2("");
        if (IsMonolithic)
        {
            lib_deb_name = LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + wxT_2("d") + LibSuffix;
            lib_rel_name = LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibSuffix;
        }
        else /* Check for wxcore*/
        {
            lib_deb_name = LibPrefix + wxT_2("wxbase") + LibWxVer + LibUnicSuffix + wxT_2("d") + LibSuffix;
            lib_rel_name = LibPrefix + wxT_2("wxbase") + LibWxVer + LibUnicSuffix + LibSuffix;
        }
        /* Check whether the libraries exist or not */
        if (WizType == wizProject)
        {
            local chk_debug = Wizard.GetWantDebug();
            local chk_release = Wizard.GetWantRelease();
            if (!IO.FileExists(LibPath + wxT_2("/") + lib_deb_name) && (chk_debug == true))
            {
                // alarm!
                if (!IO.FileExists(LibPath + wxT_2("/") + lib_rel_name))
                {
                    if (Message(wxT_2("A matching Debug configuration cannot be found in the wxWidgets directory " +
                                "you specified.\n" +
                                "This means that Debug target of your project will not build.\n\n" +
                                "Are you sure you want to continue with these settings?"),
                                wxT_2("Warning"), wxYES_NO) == wxID_NO)
                    {
                        return false;
                    }
                }
                else
                {
                    if (Message(wxT_2("A matching Debug configuration cannot be found in the wxWidgets directory " +
                                "you specified.\n" +
                                "Would you like to link this target against the release binaries instead?\n" +
                                "(Debugging the executable will still be possible.)"),
                                wxT_2("Warning"), wxYES_NO) == wxID_YES)
                    {
                        IsPartialDebug = true;
                    }
                    else if (Message(wxT_2("This means that Debug target of your project will not build.\n\n" +
                                     "Are you sure you want to continue with these settings?"),
                                     wxT_2("Warning"), wxYES_NO) == wxID_NO)
                    {
                        return false;
                    }
                }
            }
            if (!IO.FileExists(LibPath + wxT_2("/") + lib_rel_name) && (chk_release == true))
            {
                // alarm!
                if (Message(wxT_2("A matching Release configuration cannot be found in the wxWidgets directory " +
                            "you specified.\n" +
                            "This means that Release target of your project will not build.\n\n" +
                            "Are you sure you want to continue with these settings?"),
                            wxT_2("Warning"), wxYES_NO) == wxID_NO)
                {
                    return false;
                }
            }
        }
        else
        {
            local libname;
            if (Wizard.GetTargetEnableDebug())
                libname = LibPath + wxT_2("/") + lib_deb_name;
            else
                libname = LibPath + wxT_2("/") + lib_rel_name;
            if (!IO.FileExists(libname))
            {
                if (Message(wxT_2("A matching configuration cannot be found in the wxWidgets directory " +
                            "you specified.\n" +
                            "This means that this target of your project will not build.\n\n" +
                            "Are you sure you want to continue with these settings?"),
                            wxT_2("Warning"), wxYES_NO) == wxID_NO)
                {
                    return false;
                }
            }
        }

        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvctk"))
         || GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc8"))
         || GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc10")))
        {
            local msg = wxT_2("Wizard will setup the Project in Multi-threaded Dynamic CRT mode by default.\n\n");
            msg = msg + wxT_2("Click Yes to continue with Multi-threaded Dynamic CRT mode\n\n");
            msg = msg + wxT_2("Click No to continue with Multi-threaded Static CRT mode");
            local thread = Message(msg, wxT_2("wxWidgets Wizard"), wxICON_QUESTION | wxYES_NO);
            if (thread == wxID_YES)
                multi_thread_dynamic = true;
            else
                multi_thread_dynamic = false;
        }
    }

    return true;
}

////////////////////////////////////////////////////////////////////////////////
// wxWidgets' settings (unix page)
////////////////////////////////////////////////////////////////////////////////

function OnEnter_WxConfUnix(fwd)
{
    if (fwd)
    {
        Wizard.SetRadioboxSelection(wxT_2("m_radioBoxWxChoice"), ChoiceWxUnixLib);
        Wizard.CheckCheckbox(wxT_2("chkWxConfSo"), IntToBool(ConfigManager.Read(wxT_2("/wx_project_wizard/dll"), 0)));
        Wizard.CheckCheckbox(wxT_2("chkWxConfUnicode"), IntToBool(ConfigManager.Read(wxT_2("/wx_project_wizard/unicode"), 0)));
        Wizard.CheckCheckbox(wxT_2("chkWxUnixConfPCH"), IntToBool(ConfigManager.Read(wxT_2("/wx_project_wizard/pch"), 0)));
        OnClick_m_radioBoxWxChoice();
    }
    return true;
}

function OnLeave_WxConfUnix(fwd)
{
    if (fwd)
    {
        ChoiceWxUnixLib = Wizard.GetRadioboxSelection(wxT_2("m_radioBoxWxChoice"));
        if (ChoiceWxUnixLib == 1)
        {
            IsDLL = Wizard.IsCheckboxChecked(wxT_2("chkWxConfSo"));
            IsUnicode = Wizard.IsCheckboxChecked(wxT_2("chkWxConfUnicode"));
        }
        IsEmpty = Wizard.IsCheckboxChecked(wxT_2("chkWxUnixEmpty")); // Checks option for Empty Project
        WantPCH = Wizard.IsCheckboxChecked(wxT_2("chkWxUnixConfPCH"));

        if (!GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc*")) && WantPCH)
        {
            ShowWarning(wxT_2("Precompiled headers currently only work for GNU GCC.\n" +
                        "They are disabled for all other compilers."));
            WantPCH = false;
        }

        if (WxVersion == 0)
            LibWxVer = wxT_2("2.6");
        else if (WxVersion == 1)
            LibWxVer = wxT_2("2.8");
        else if (WxVersion == 2)
            LibWxVer = wxT_2("3.0");

        // Ask the user whether wizard shall add PCH support when empty Project is selected
        if (IsEmpty && WantPCH)
        {
            local msg = wxT_2("You have selected PCH support for Empty project.\n");
            msg = msg + wxT_2("Wizard will NOT add PCH support as it can't be added without adding any file.\n\n");
            msg = msg + wxT_2("Please add the PCH header later to Project");
            ShowInfo(msg);
            WantPCH = false; // Sorry! Wizard can't add PCH support
        }
        PCHFileName = wxT_2("wx_pch.h");

        // Now write the setting to Configuration
        ConfigManager.Write(wxT_2("/wx_project_wizard/dll"), BoolToInt(IsDLL));
        ConfigManager.Write(wxT_2("/wx_project_wizard/unicode"), BoolToInt(IsUnicode));
        ConfigManager.Write(wxT_2("/wx_project_wizard/pch"), BoolToInt(WantPCH));
    }
    return true;
}

function OnClick_m_radioBoxWxChoice()
{
    ChoiceWxUnixLib = Wizard.GetRadioboxSelection(wxT_2("m_radioBoxWxChoice"));
    if (ChoiceWxUnixLib == 0) // Means default choice
    {
        Wizard.EnableWindow(wxT_2("chkWxConfSo"), false);
        Wizard.EnableWindow(wxT_2("chkWxConfUnicode"), false);
    }
    else
    {
        Wizard.EnableWindow(wxT_2("chkWxConfSo"), true);
        Wizard.EnableWindow(wxT_2("chkWxConfUnicode"), true);
    }
}

// -----------------------------------------------------------------------------
// each time, return a string of the form "filename.ext;contents"
// you can change the return string based on <file_index>
// return an empty string to denote that no more files are to be generated
function GetGeneratedFile(file_index)
{
    if (!IsEmpty)
    {
        local Prefix = GetFixedProjectName(Wizard.GetProjectName());
        if (file_index == 0)
            return Prefix + wxT_2("App.h") + wxT_2(";") + GenerateHeader(file_index);
        else if (file_index == 1)
            return Prefix + wxT_2("App.cpp") + wxT_2(";") + GenerateSource(file_index);
        else if (file_index == 2)
            return Prefix + wxT_2("Main.h") + wxT_2(";") + GenerateHeader(file_index);
        else if (file_index == 3)
            return Prefix + wxT_2("Main.cpp") + wxT_2(";") + GenerateSource(file_index);
        if (GuiBuilder == 1)
        {
            if (file_index == 4)
            {
                if (GuiAppType == 0)
                    return wxT_2("wxsmith/") + Prefix + wxT_2("dialog.wxs") + wxT_2(";") + GenerateSource(file_index);
                else
                    return wxT_2("wxsmith/") + Prefix + wxT_2("frame.wxs") + wxT_2(";") + GenerateSource(file_index);
            }
            if (file_index == 5 && WantPCH)
                return wxT_2("wx_pch.h") + wxT_2(";") + GenerateHeader(file_index);
        }
        else
        {
            if (file_index == 4 && WantPCH)
                return wxT_2("wx_pch.h") + wxT_2(";") + GenerateHeader(file_index);
        }
    }
    return wxT_2(""); // no more generated files
}

// return the files this project contains
function GetFilesDir()
{
    local result = wxT_2("");
    if (!IsEmpty) // Checks whether user wants Empty Project or not
    {
        if (PLATFORM == PLATFORM_MSW)
            result = wxT_2("wxwidgets/rc;");
        if (GuiBuilder == 2)
        {
            if (GuiAppType == 0)
                result = result + wxT_2("wxwidgets/wxfb/dialog;");
            else if (GuiAppType == 1)
                result = result + wxT_2("wxwidgets/wxfb/frame;");
        }
    }
    return result;
}

// setup the already created project
function SetupProject(project)
{
    local libdir;
    SetupAddlLibs();
    // set project options
    if (PLATFORM != PLATFORM_MSW)
    {
        if (ChoiceWxUnixLib == 0)
        {
            project.AddCompilerOption(wxT_2("`wx-config --cflags`"));
            project.AddLinkerOption(wxT_2("`wx-config --libs`"));
        }
        else
        {
            local target = project.GetBuildTarget(Wizard.GetDebugName());
            if (!IsNull(target))
                SetupTarget(target, true);
            local target = project.GetBuildTarget(Wizard.GetReleaseName());
            if (!IsNull(target))
                SetupTarget(target, false);
        }

        // Now enable PCH
        if (WantPCH && GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc*")))
        {
            local pchfile = project.GetFileByFilename(PCHFileName, true, true);
            if (!IsNull(pchfile))
            {
                pchfile.compile = true;
                pchfile.link = false;
                pchfile.weight = 0;
                project.SetModeForPCH(pchSourceDir); // pch dir
                project.AddCompilerOption(wxT_2("-Winvalid-pch"));
                project.AddCompilerOption(wxT_2("-include ") + PCHFileName);
                project.AddCompilerOption(wxT_2("-DWX_PRECOMP"));
            }
        }
    }
    else
    {
        project.AddIncludeDir(WxPath + wxT_2("/include"));
        project.AddResourceIncludeDir(WxPath + wxT_2("/include"));
        libdir = LibPath + wxT_2("/msw");
        if (IsUnicode)
            libdir = libdir + wxT_2("u");

        /* Add standard and special compiler options and libraries */
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc*")))
        {
            project.AddCompilerOption(wxT_2("/DWIN32"));
            project.AddCompilerOption(wxT_2("/D__WIN32__"));
            project.AddCompilerOption(wxT_2("/D__WXMSW__"));
            if (IsDLL)
                project.AddCompilerOption(wxT_2("/DWXUSINGDLL"));
            if (IsUnicode)
                project.AddCompilerOption(wxT_2("/DwxUSE_UNICODE"));
            project.AddCompilerOption(wxT_2("/D_WINDOWS"));
            project.AddLinkerOption(wxT_2("/INCREMENTAL:NO"));
        }
        else if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc*")))
        {
            project.AddCompilerOption(wxT_2("-pipe"));
            project.AddCompilerOption(wxT_2("-mthreads"));
            project.AddLinkerOption(wxT_2("-mthreads"));
            project.AddCompilerOption(wxT_2("-D__GNUWIN32__"));
            project.AddCompilerOption(wxT_2("-D__WXMSW__"));
            if (IsDLL)
                project.AddCompilerOption(wxT_2("-DWXUSINGDLL"));
            if (IsUnicode)
                project.AddCompilerOption(wxT_2("-DwxUSE_UNICODE"));
        }
        else if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("bcc*")))
        {
            project.AddCompilerOption(wxT_2("-D__WXMSW__"));
            if (IsDLL)
                project.AddCompilerOption(wxT_2("-DWXUSINGDLL"));
            if (IsUnicode)
                project.AddCompilerOption(wxT_2("-DUNICODE"));
            project.AddCompilerOption(wxT_2("-q"));
            project.AddCompilerOption(wxT_2("-c"));
            project.AddCompilerOption(wxT_2("-P"));
            project.AddCompilerOption(wxT_2("-tWR"));
            project.AddCompilerOption(wxT_2("-tWM"));
            project.AddCompilerOption(wxT_2("-a8"));
            project.AddLinkLib(wxT_2("import32.lib"));
            project.AddLinkLib(wxT_2("cw32mti.lib"));
            project.AddLinkLib(wxT_2("ole2w32.lib"));
            project.AddLinkerOption(wxT_2("-Tpe"));
            project.AddLinkerOption(wxT_2("-aa"));
        }
        else if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("ow")))
        {
            project.AddCompilerOption(wxT_2("-d__WXMSW__"));
            if (IsDLL)
                project.AddCompilerOption(wxT_2("-dWXUSINGDLL"));
            if (IsUnicode)
                project.AddCompilerOption(wxT_2("-dUNICODE"));
            project.AddCompilerOption(wxT_2("-bm"));
            project.AddCompilerOption(wxT_2("-br"));
            project.AddCompilerOption(wxT_2("-bt=nt"));
            project.AddCompilerOption(wxT_2("-zq"));
            project.AddCompilerOption(wxT_2("-xr"));
            project.AddCompilerOption(wxT_2("-xs"));
            project.AddCompilerOption(wxT_2("-wcd=549"));
            project.AddCompilerOption(wxT_2("-wcd=656"));
            project.AddCompilerOption(wxT_2("-wcd=657"));
            project.AddCompilerOption(wxT_2("-wcd=667"));
        }
        // Please remember that the following code have been added separately as it is not tested with MSVC 6
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvctk"))
            || GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc8"))
            || GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc10")))
        {
            project.AddCompilerOption(wxT_2("/EHs"));
            project.AddCompilerOption(wxT_2("/EHc"));
            project.AddCompilerOption(wxT_2("/D_CRT_SECURE_DEPRECATE"));
            project.AddCompilerOption(wxT_2("/D_CRT_NONSTDC_NO_DEPRECATE"));
            project.AddCompilerOption(wxT_2("/D_CRT_SECURE_NO_WARNINGS"));
            project.AddLinkerOption(wxT_2("/SUBSYSTEM:WINDOWS"));
            project.AddLinkLib(wxT_2("winmm.lib"));
            project.AddLinkLib(wxT_2("rpcrt4.lib"));
        }
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc8")))
        {
            project.AddCompilerOption(wxT_2("/Zc:wchar_t"));
            project.AddCompilerOption(wxT_2("/D_VC80_UPGRADE=0x0600"));
        }
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc10")))
        {
            project.AddCompilerOption(wxT_2("/Zc:wchar_t"));
            project.AddCompilerOption(wxT_2("/Zc:auto"));
        }

        if (!IsDLL)
        {
            project.AddLinkLib(LibPrefix + wxT_2("kernel32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("user32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("gdi32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("winspool") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("comdlg32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("advapi32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("gdi32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("shell32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("ole32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("oleaut32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("uuid") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("comctl32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("wsock32") + LibSuffix);
            project.AddLinkLib(LibPrefix + wxT_2("odbc32") + LibSuffix);
        }
    }

    // (WxVersion < 2) == (wxWidgets <= 2.8.x)
    if (WxVersion < 2 && GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc*")))
        project.AddCompilerOption(wxT_2("[[if (GetCompilerFactory().GetCompilerVersionString(wxT_2(\"gcc\")) >= wxT_2(\"4.8.0\")) print(wxT_2(\"-Wno-unused-local-typedefs\"));]]"));

    // enable PCH
    if (WantPCH && GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc*")))
    {
        local pchfile = project.GetFileByFilename(PCHFileName, true, true);
        if (!IsNull(pchfile))
        {
            pchfile.compile = true;
            pchfile.link = false;
            pchfile.weight = 0;
            project.SetModeForPCH(pchSourceDir); // pch dir
            project.AddCompilerOption(wxT_2("-Winvalid-pch"));
            project.AddCompilerOption(wxT_2("-include ") + PCHFileName);
            project.AddCompilerOption(wxT_2("-DWX_PRECOMP"));
        }
    }
    // For other compilers, different approach has been used
    else if (WantPCH && GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("bcc*")))
    {
        project.AddCompilerOption(wxT_2("-H"));
        project.AddCompilerOption(wxT_2("-DWX_PRECOMP"));
    }
    else if (WantPCH && (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvctk"))
                        || GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc8"))
                        || GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc10"))))
    {
        project.AddCompilerOption(wxT_2("/FI\"") + PCHFileName + wxT_2("\""));
        project.AddCompilerOption(wxT_2("/Yc\"") + PCHFileName + wxT_2("\""));
    }

    // enable compiler warnings (project-wide)
    WarningsOn(project, Wizard.GetCompilerID());

    // Debug
    local target = project.GetBuildTarget(Wizard.GetDebugName());
    if (!IsNull(target))
        SetupTarget(target, true);

    // Release
    target = project.GetBuildTarget(Wizard.GetReleaseName());
    if (!IsNull(target))
        SetupTarget(target, false);

    if (GuiBuilder == 1)
    {
        if ("WxsAddWxExtensions" in getroottable())
        {
            // Adding extra bindings for wxSmith
            local Prefix = GetFixedProjectName(Wizard.GetProjectName());
            local WxsFileName = wxT_2("");

            if ( GuiAppType==0 )
                WxsFileName = wxT_2("dialog.wxs");
            else
                WxsFileName = wxT_2("frame.wxs");

            WxsAddWxExtensions(
                project,
                Prefix+wxT_2("App.cpp"),
                Prefix+wxT_2("Main.cpp"),
                Prefix+wxT_2("Main.h"),
                wxT_2("wxsmith/")+Prefix+WxsFileName);
        }
    }

    return true;
}

function SetupTarget(target, is_debug)
{
    if (IsNull(target))
        return false;

    local obj_output_dir, exe_file_name, exe_output_dir;
    if (WizType == wizProject)
    {
        if (is_debug)
        {
            obj_output_dir = Wizard.GetDebugObjectOutputDir();
            exe_output_dir = Wizard.GetDebugOutputDir();
        }
        else
        {
            obj_output_dir = Wizard.GetReleaseObjectOutputDir();
            exe_output_dir = Wizard.GetReleaseOutputDir();
        }
        exe_file_name = Wizard.GetProjectName();
    }
    else if (WizType == wizTarget)
    {
        obj_output_dir = Wizard.GetTargetObjectOutputDir();
        exe_output_dir = Wizard.GetTargetOutputDir();
        exe_file_name = target.GetParentProject().GetTitle();
    }

    if (is_debug)
    {
        if (DebugTarget == 0)
            target.SetTargetType(ttConsoleOnly);
        else
            target.SetTargetType(ttExecutable);
    }
    else
    {
        if (ReleaseTarget == 0)
            target.SetTargetType(ttConsoleOnly);
        else
            target.SetTargetType(ttExecutable);
    }

    target.SetOutputFilename(exe_output_dir + exe_file_name + DOT_EXT_EXECUTABLE);

    if (is_debug)
        DebugSymbolsOn(target, Wizard.GetCompilerID());
    else
        OptimizationsOn(target, Wizard.GetCompilerID());

    target.SetOptionRelation(ortLinkerOptions, orPrependToParentOptions);
    if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc*")))
        is_debug = ChkWxDebug && is_debug;

    if (!is_debug || IsPartialDebug)
        LibDebugSuffix = wxT_2("");

    /* For Linux / Mac */
    if (PLATFORM != PLATFORM_MSW)
    {
        if (ChoiceWxUnixLib == 1)
        {
            local flags;
            flags = wxT_2("`wx-config ");
            flags = flags + wxT_2(" --version=") + LibWxVer;
            flags = flags + (IsDLL == false ? wxT_2(" --static=yes") : wxT_2(" --static=no"));
            flags = flags + (IsUnicode == true ? wxT_2(" --unicode=yes") : wxT_2(" --unicode=no"));
            if (is_debug)
            {
                target.AddCompilerOption(flags + wxT_2(" --debug=yes --cflags`"));
                target.AddLinkerOption(flags + wxT_2(" --debug=yes --libs`"));
            }
            if (!is_debug)
            {
                target.AddCompilerOption(flags + wxT_2(" --debug=no --cflags`"));
                target.AddLinkerOption(flags + wxT_2(" --debug=no --libs`"));
            }
        }

        if (PLATFORM == PLATFORM_MAC)
        {
            // still need the resource fork hack to run unbundled wxWidgets applications:
            local rezflags = wxT_2("/Developer/Tools/Rez -d __DARWIN__ -t APPL Carbon.r -o");

            if (!IsNull(target))
            {
                target.AddCommandsAfterBuild(rezflags + wxT_2(" $(TARGET_OUTPUT_FILE)"));
            }
        }
    }
    else if (PLATFORM == PLATFORM_MSW)
    {
        local libdir = LibPath + wxT_2("/msw");
        if (IsUnicode)
            libdir = libdir + wxT_2("u");
        if (is_debug && !IsPartialDebug)
        {
            target.AddIncludeDir(libdir + wxT_2("d"));
            target.AddLibDir(LibPath);
            target.AddResourceIncludeDir(libdir + wxT_2("d"));
        }
        else
        {
            target.AddIncludeDir(libdir);
            target.AddLibDir(LibPath);
            target.AddResourceIncludeDir(libdir);
        }

        /* Modified and added */
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc*")) && is_debug && !IsPartialDebug)
        {
            target.AddCompilerOption(wxT_2("/D__WXDEBUG__")); // For Debug Build
        }
        else if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("gcc*")) && is_debug && !IsPartialDebug)
        {
            target.AddCompilerOption(wxT_2("-D__WXDEBUG__"));
        }
        else if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("bcc*")))
        {
            if (is_debug)
            {
                if (!IsPartialDebug)
                {
                    target.AddCompilerOption(wxT_2("-D__WXDEBUG__"));
                }
                target.AddLinkerOption(wxT_2("-v"));
            }
            if (WantPCH) // Add support for PCH
                target.AddCompilerOption(wxT_2("-H -H=") + obj_output_dir + exe_file_name + wxT_2(".csm"));
        }
        else if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("ow")))
        {
            if (is_debug)
            {
                if (!IsPartialDebug)
                {
                    target.AddCompilerOption(wxT_2("-d__WXDEBUG__"));
                }
                target.AddLinkerOption(wxT_2("-d2"));
            }
        }

        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvctk")))
        {
            if (is_debug)
            {
                target.AddLinkerOption(wxT_2("/NODEFAULTLIB:libcpmtd.lib"));
                target.AddLinkLib(wxT_2("msvcprtd.lib"));
            }
            else
            {
                target.AddLinkerOption(wxT_2("/NODEFAULTLIB:libcpmt.lib"));
                target.AddLinkLib(wxT_2("msvcprt.lib"));
            }
        }
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvctk"))
            || GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc8"))
            || GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc10")))
        {
            if(WantPCH) // Add support for PCH
                target.AddCompilerOption(wxT_2("/Fp\"") + obj_output_dir + exe_file_name + wxT_2(".pch\""));

            if (is_debug)
            {
                if (multi_thread_dynamic)
                {
                    target.AddCompilerOption(wxT_2("/MDd"));
                    target.AddLinkerOption(wxT_2("/NODEFAULTLIB:libcmtd.lib"));
                    target.AddLinkerOption(wxT_2("/NODEFAULTLIB:msvcrt.lib"));
                    target.AddLinkLib(wxT_2("msvcrtd.lib"));
                }
                else
                {
                    target.AddCompilerOption(wxT_2("/MTd"));
                }

                target.AddCompilerOption(wxT_2("/D_DEBUG"));
            }
            else
            {
                if (multi_thread_dynamic)
                {
                    target.AddCompilerOption(wxT_2("/MD"));
                    target.AddLinkerOption(wxT_2("/NODEFAULTLIB:libcmt.lib"));
                    target.AddLinkLib(wxT_2("msvcrt.lib"));
                }
                else
                {
                    target.AddCompilerOption(wxT_2("/MT"));
                }

                target.AddCompilerOption(wxT_2("/O2"));
            }
        }
        if (GetCompilerFactory().CompilerInheritsFrom(Wizard.GetCompilerID(), wxT_2("msvc8")))
        {
            target.RemoveCompilerOption(wxT_2("/Og")); // Deprecated option in MSVC 8
            /* Now embed the generated manifest file */
            target.AddCommandsAfterBuild(wxT_2("mt.exe /nologo /manifest \"") + exe_output_dir + exe_file_name + wxT_2(".exe.manifest\" /outputresource:\"") + exe_output_dir + exe_file_name + wxT_2(".exe\";1"));
        }
        /* End Modification*/

        /* Now Add the required Libraries */
        if (IsMonolithic)
        {
            target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + LibSuffix);
            if (!IsDLL)
            {
                target.AddLinkLib(LibPrefix + wxT_2("wxpng") + LibDebugSuffix + LibSuffix);
                target.AddLinkLib(LibPrefix + wxT_2("wxjpeg") + LibDebugSuffix + LibSuffix);
                target.AddLinkLib(LibPrefix + wxT_2("wxtiff") + LibDebugSuffix + LibSuffix);
                target.AddLinkLib(LibPrefix + wxT_2("wxzlib") + LibDebugSuffix + LibSuffix);
            }
        }
        else
        {
            // Check and add additional libraries
            if (LibWxRichText) target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_richtext") + LibSuffix);
            if (LibWxXrc) target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_xrc") + LibSuffix);
            if (LibWxAdvUI) target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_aui") + LibSuffix);
            if (LibWxOdbc)
            {
                target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_dbgrid") + LibSuffix);
                target.AddLinkLib(LibPrefix + wxT_2("wxbase") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_odbc") + LibSuffix);
            }
            if (LibWxMedia) target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_media") + LibSuffix);
            if (LibWxNet) target.AddLinkLib(LibPrefix + wxT_2("wxbase") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_net") + LibSuffix);
            if (LibWxOpengl) target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_gl") + LibSuffix);
            if (LibWxQa) target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_qa") + LibSuffix);
            // Now comes the core libraries
            if (LibWxXml) target.AddLinkLib(LibPrefix + wxT_2("wxbase") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_xml") + LibSuffix);
            if (LibWxAdv) target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_adv") + LibSuffix);
            if (LibWxHtml) target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_html") + LibSuffix);
            target.AddLinkLib(LibPrefix + wxT_2("wxmsw") + LibWxVer + LibUnicSuffix + LibDebugSuffix + wxT_2("_core") + LibSuffix);
            target.AddLinkLib(LibPrefix + wxT_2("wxbase") + LibWxVer + LibUnicSuffix + LibDebugSuffix + LibSuffix);
            target.AddLinkLib(LibPrefix + wxT_2("wxpng") + LibDebugSuffix + LibSuffix);
            if (LibWxJpeg) target.AddLinkLib(LibPrefix + wxT_2("wxjpeg") + LibDebugSuffix + LibSuffix);
            if (LibWxTiff) target.AddLinkLib(LibPrefix + wxT_2("wxtiff") + LibDebugSuffix + LibSuffix);
            target.AddLinkLib(LibPrefix + wxT_2("wxzlib") + LibDebugSuffix + LibSuffix);
            if (LibWxRegex) target.AddLinkLib(LibPrefix + wxT_2("wxregex") + LibUnicSuffix + LibDebugSuffix + LibSuffix);
            if (LibWxExpat) target.AddLinkLib(LibPrefix + wxT_2("wxexpat") + LibDebugSuffix + LibSuffix);
        }
    }
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// Add additional wxWidgets libraries (for Windows)
////////////////////////////////////////////////////////////////////////////////

function OnGetNextPage_WxConf()
{
    IsAdvOpt = Wizard.IsCheckboxChecked(wxT_2("chkWxConfAdvOpt"));
    IsMonolithic = Wizard.IsCheckboxChecked(wxT_2("chkWxConfMono"));
    if (IsAdvOpt)
        return wxT_2("WxConfAdvOpt");
    if (!IsMonolithic)
        return wxT_2("WxAddLib");
    return wxT_2("");
}

////////////////////////////////////////////////////////////////////////////////
// Set appropriate Global Variables for Target type
////////////////////////////////////////////////////////////////////////////////

function OnEnter_WxConfAdvOpt(fwd)
{
    if (fwd)
    {
        Wizard.EnableWindow(wxT_2("RadioBoxDebug"), Wizard.GetWantDebug());
        Wizard.EnableWindow(wxT_2("RadioBoxRelease"), Wizard.GetWantRelease());
    }
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// Set appropriate Global Variables for Target type
////////////////////////////////////////////////////////////////////////////////

function OnLeave_WxConfAdvOpt(fwd)
{
    if (fwd)
    {
        ChkWxDebug = Wizard.IsCheckboxChecked(wxT_2("chkWxDebug"));
        DebugTarget = Wizard.GetRadioboxSelection(wxT_2("RadioBoxDebug"));
        ReleaseTarget = Wizard.GetRadioboxSelection(wxT_2("RadioBoxRelease"));
        if (!ChkWxDebug) LibDebugSuffix = wxT_2("");
    }
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// Check for next wizard page after Target type
////////////////////////////////////////////////////////////////////////////////

function OnGetNextPage_WxConfAdvOpt()
{
    if (IsMonolithic)
        return wxT_2("");
    else
        return wxT_2("WxAddLib");
}

////////////////////////////////////////////////////////////////////////////////
// Set global variables for additional lib wizard page (for Windows)
////////////////////////////////////////////////////////////////////////////////

function OnGetPrevPage_WxAddLib()
{
    if (IsAdvOpt) // IsAdvOpt - Refers to Target Type
        return wxT_2("WxConfAdvOpt");
    else
        return wxT_2("WxConf");
}

////////////////////////////////////////////////////////////////////////////////
// Set global variables for additional lib wizard page (for Windows)
////////////////////////////////////////////////////////////////////////////////

function OnEnter_WxAddLib(fwd)
{
    return true;
}

function OnLeave_WxAddLib(fwd)
{
    if (fwd)
    {
        AddlLibList = Wizard.GetListboxStringSelections(wxT_2("lstWxLibs"));
    }
    return true;
}

// -----------------------------------------------------------------------------
// return the template's filename, appending <dot_ext> as an extension (must include the dot)
function GetTemplateFile(index)
{
    local template_file = wxT_2("");
    if (GuiBuilder == 1)
    {
        if (index == 0)
            template_file = wxT_2("wxwidgets/common/app.h");
        else if (index == 1)
            template_file = wxT_2("wxwidgets/wxsmith/app.cpp");
        else if (index == 2)
            template_file = wxT_2("wxwidgets/wxsmith/main.h");
        else if (index == 3)
            template_file = wxT_2("wxwidgets/wxsmith/main.cpp");
        else if (index == 4)
            template_file = wxT_2("wxwidgets/wxsmith/resource.wxs");
        else if (index == 5 && WantPCH)
            template_file = wxT_2("wxwidgets/pch/wx_pch.h");
    }
    else
    {
        if (index == 0)
            template_file = wxT_2("wxwidgets/common/app.h");
        else if (index == 1)
            template_file = wxT_2("wxwidgets/common/app.cpp");
        else if (index == 2)
            template_file = wxT_2("wxwidgets/common/main.h");
        else if (index == 3)
            template_file = wxT_2("wxwidgets/common/main.cpp");
        else if (index == 4 && WantPCH)
            template_file = wxT_2("wxwidgets/pch/wx_pch.h");
    }
    return template_file;
}

// -----------------------------------------------------------------------------
// return the header contents string
function GenerateHeader(index)
{
    local path = Wizard.FindTemplateFile(GetTemplateFile(index));
    local buffer = IO.ReadFileContents(path);

    return SubstituteMacros(buffer);
}

// -----------------------------------------------------------------------------
// return the implementation contents string
function GenerateSource(index)
{
    local path = Wizard.FindTemplateFile(GetTemplateFile(index));
    local buffer = IO.ReadFileContents(path);

    return SubstituteMacros(buffer);
}

// -----------------------------------------------------------------------------
// substitute all plugin macros in <buffer>
function SubstituteMacros(buffer)
{
    // handle [IF] / [ENDIF] pairs
    if (GuiBuilder == 0)
    {
        if (GuiAppType == 0)
        {
            buffer  = HandleDirective(buffer, wxT_2("WXDIALOG"), true);
            buffer  = HandleDirective(buffer, wxT_2("WXFRAME"), false);
        }
        else if (GuiAppType == 1)
        {
            buffer  = HandleDirective(buffer, wxT_2("WXDIALOG"), false);
            buffer  = HandleDirective(buffer, wxT_2("WXFRAME"), true);
        }
        buffer  = HandleDirective(buffer, wxT_2("NONE"), true);
        buffer  = HandleDirective(buffer, wxT_2("WXFB"), false);
    }
    else if (GuiBuilder == 1)
    {
        if (GuiAppType == 0)
        {
            buffer  = HandleDirective(buffer, wxT_2("WXDIALOG"), true);
            buffer  = HandleDirective(buffer, wxT_2("WXFRAME"), false);
        }
        else if (GuiAppType == 1)
        {
            buffer  = HandleDirective(buffer, wxT_2("WXDIALOG"), false);
            buffer  = HandleDirective(buffer, wxT_2("WXFRAME"), true);
        }
    }
    else if (GuiBuilder == 2)
    {
        if (GuiAppType == 0)
        {
            buffer  = HandleDirective(buffer, wxT_2("WXDIALOG"), true);
            buffer  = HandleDirective(buffer, wxT_2("WXFRAME"), false);
        }
        else if (GuiAppType == 1)
        {
            buffer  = HandleDirective(buffer, wxT_2("WXDIALOG"), false);
            buffer  = HandleDirective(buffer, wxT_2("WXFRAME"), true);
        }
        buffer  = HandleDirective(buffer, wxT_2("NONE"), false);
        buffer  = HandleDirective(buffer, wxT_2("WXFB"), true);
    }
    buffer = HandleDirective(buffer, wxT_2("WINDOWS"), (PLATFORM == PLATFORM_MSW ? true : false));

    // create class name from project name which is valid c++ identifier
    local Prefix = GetFixedProjectName(Wizard.GetProjectName());
    local PchInclude = WantPCH ? ( wxT_2("#include \"") + PCHFileName + wxT_2("\"\n") ) : wxT_2("");

    // macros substitution
    buffer.Replace(wxT_2("[PROJECT_HDR]"), Prefix.Upper() );
    buffer.Replace(wxT_2("[PROJECT_NAME]"), Wizard.GetProjectName());
    buffer.Replace(wxT_2("[FILENAME_PREFIX]"), Prefix);
    buffer.Replace(wxT_2("[CLASS_PREFIX]"), Prefix);
    buffer.Replace(wxT_2("[AUTHOR_NAME]"), ProjAuthor);
    buffer.Replace(wxT_2("[AUTHOR_EMAIL]"), ProjEmail);
    buffer.Replace(wxT_2("[AUTHOR_WWW]"), ProjWebsite);
    buffer.Replace(wxT_2("[NOW]"), ReplaceMacros(wxT_2("$(TODAY)"), false));
    buffer.Replace(wxT_2("[PCH_INCLUDE]"), PchInclude);

    return buffer;
}

// -----------------------------------------------------------------------------
// if <enabled> is true, removes the [IF <directive>] and [ENDIF <directive>]
// macros.
// if <enabled> is false, removes everything enclosed by the [IF <directive>]
// and [ENDIF <directive>] macros (including them).
function HandleDirective(buffer, directive, enabled)
{
    local dir_if = wxT_2("[IF ") + directive + wxT_2("]");
    local dir_endif = wxT_2("[ENDIF ") + directive + wxT_2("]");

    while ( true )
    {
        local findStart = buffer.Find(dir_if);
        if (findStart == -1)
            return buffer;

        local findEnd = buffer.Find(dir_endif);
        if (findEnd == -1 || findEnd <= findStart)
            return buffer;

        // look for [ELSE]
        local block = buffer.Mid(findStart, findEnd - findStart);
        local findElse = block.Find(wxT_2("[ELSE]")); // findElse is in "local scope", i.e. offset from findStart

        if (!enabled)
        {
            if (findElse == -1)
            {
                // remove whole section
                buffer.Remove(findStart, (findEnd - findStart) + dir_endif.Length());
            }
            else
            {
                // remove [ENDIF]
                buffer.Remove(findEnd, dir_endif.Length());
                // remove from [IF] to [ELSE] (including)
                buffer.Remove(findStart, findElse + 6); // 6 is the [ELSE] size
            }
        }
        else
        {
            if (findElse == -1)
            {
                // just remove the directives
                // we must remove the [ENDIF] first because if we removed the [IF] it would
                // render the findEnd index invalid!
                buffer.Remove(findEnd, dir_endif.Length());
                buffer.Remove(findStart, dir_if.Length());
            }
            else
            {
                // remove from [ELSE] to [ENDIF]
                local start = findStart + findElse;
                buffer.Remove(start, (findEnd - start) + dir_endif.Length());
                // remove from [IF]
                buffer.Remove(findStart, dir_if.Length());
            }
        }
    }

    return buffer;
}

function IntToBool(val)
{
    return (val == 0 ? false : true);
}

function BoolToInt(val)
{
    return (val ? 1 : 0);
}

function SetupAddlLibs()
{
    // Now set these variable values based on library selections
    if (!AddlLibList.IsEmpty())
    {
        local tempLibArray = ::wxArrayString();
        tempLibArray = GetArrayFromString(AddlLibList, wxT_2(";"), false);

        LibWxRichText = ((WxVersion > 0) && (tempLibArray.Index(wxT_2("wxRichText")) >=0));
        LibWxAdvUI = ((WxVersion > 0) && (tempLibArray.Index(wxT_2("wxAui")) >=0));
        LibWxXrc = (tempLibArray.Index(wxT_2("wxXrc")) >=0);
        LibWxOdbc = ((tempLibArray.Index(wxT_2("wxDbGrid")) >=0) || (tempLibArray.Index(wxT_2("wxOdbc")) >=0));
        LibWxMedia = (tempLibArray.Index(wxT_2("wxMedia")) >=0);;
        LibWxNet = (tempLibArray.Index(wxT_2("wxNet")) >=0);
        LibWxOpengl = (tempLibArray.Index(wxT_2("wxGl")) >=0);
        LibWxQa = (tempLibArray.Index(wxT_2("wxQa")) >=0);
        LibWxXml = (tempLibArray.Index(wxT_2("wxXml")) >=0) || LibWxRichText || LibWxXrc;
        LibWxAdv = (tempLibArray.Index(wxT_2("wxAdv")) >=0) || LibWxRichText || LibWxXrc || LibWxOdbc;
        LibWxHtml = (tempLibArray.Index(wxT_2("wxHtml")) >=0) || LibWxRichText || LibWxXrc;
        LibWxJpeg = (tempLibArray.Index(wxT_2("wxJpeg")) >=0);
        LibWxTiff = (tempLibArray.Index(wxT_2("wxTiff")) >=0);
        LibWxRegex = (tempLibArray.Index(wxT_2("wxRegex")) >=0);
        LibWxExpat = (tempLibArray.Index(wxT_2("wxExpat")) >=0);
    }
}
