/*
 * This file is part of the Code::Blocks IDE and licensed under the GNU General Public License, version 3
 * http://www.gnu.org/licenses/gpl-3.0.html
 *
 * $Revision: 9225 $
 * $Id: common_functions.script 9225 2013-07-22 21:11:11Z alpha0010 $
 * $HeadURL: http://svn.code.sf.net/p/codeblocks/code/branches/release-16.xx/src/plugins/scriptedwizard/resources/common_functions.script $
 */

//
// Common functions for all registered wizards
//

// Warnings On
function WarningsOn(base, compilerID)
{
    if      (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("msvc*")) )
    {
        base.AddCompilerOption(wxT_2("/W3"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("arm*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("avr*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("cygwin"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gcc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gdc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gfortran"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("icc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ppc*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("tcc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("tricore*")) )
    {
        base.AddCompilerOption(wxT_2("-Wall"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("bcc*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("dmd"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ldc")) )
    {
        base.AddCompilerOption(wxT_2("-w"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ow")) )
    {
        base.AddCompilerOption(wxT_2("-wx")); // max. warning level
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("dmc")) )
    {
        base.AddCompilerOption(wxT_2("-w-"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("clang")) )
    {
        base.AddCompilerOption(wxT_2("-Weverything"));
    }
    else if(    GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("lcc")) )
    {
        base.AddCompilerOption(wxT_2("-A"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("sdcc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("null")) )
    {
        // with SDCC all warnings are enabled by default. You can only ask for less by using --less-pedantic or --disable-warning.
        // the (pseudo) compiler "No Compiler" takes no options
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("keil*")))
    {
        base.AddCompilerOption(wxT_2("WARNINGLEVEL(2)"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("iar*")))
    {
        base.AddCompilerOption(wxT_2("--remarks"));
    }
    else
    {
        ShowWarning(wxT_2("This wizard doesn't know how to setup warning flags for this compiler.\n"));
    }
    // TODO: Other compilers, fallback?
}// WarningsOn

// Debug symbols On
function DebugSymbolsOn(base, compilerID)
{
    if      (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("msvc*")))
    {
        base.AddCompilerOption(wxT_2("/Zi"));
        base.AddCompilerOption(wxT_2("/D_DEBUG"));
        base.AddLinkerOption(wxT_2("/DEBUG"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("arm*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("avr*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("clang"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("cygwin"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("dmc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("dmd"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gcc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gdc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gfortran"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ldc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("tcc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ppc*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("tricore*")) )
    {
        base.AddCompilerOption(wxT_2("-g"));

        if  (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("dmd")))
        {
            base.AddCompilerOption(wxT_2("-debug"));
        }
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("bcc*")))
    {
        base.AddCompilerOption(wxT_2("-v"));
        base.AddLinkerOption(wxT_2("-v"));
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("icc")))
    {
        base.AddCompilerOption(wxT_2("/Zi"));
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("lcc")))
    {
        base.AddCompilerOption(wxT_2("-g2"));
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ow")))
    {
        base.AddCompilerOption(wxT_2("-d2")); // full symbolic debugging information
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("keil*")))
    {
        base.AddCompilerOption(wxT_2("DEBUG"));
        base.AddCompilerOption(wxT_2("OBJECTEXTEND"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("sdcc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("iar*")))
    {
        base.AddCompilerOption(wxT_2("--debug"));
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("null")))
    {
        // the (pseudo) compiler "No Compiler" takes no options
    }
    else
    {
        ShowWarning(wxT_2("This wizard doesn't know how to setup debug flags for this compiler.\n"));
    }
    // TODO: Other compilers, fallback?
}// DebugSymbolsOn

// Optimizations On
function OptimizationsOn(base, compilerID)
{
    if      (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("msvc*")))
    {
        if (   !GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("msvc8"))
            && !GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("msvc10")) )
        {

            base.AddCompilerOption(wxT_2("/Og"));  // Deprecated in MSVC 8 and later (MSVC 10)
        }
        base.AddCompilerOption(wxT_2("/Ox"));
        base.AddCompilerOption(wxT_2("/DNDEBUG"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("arm*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("bcc*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("clang"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("cygwin"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gcc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gdc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gfortran"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ifclin"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ldc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ppc*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("tricore*")) )
    {
        base.AddCompilerOption(wxT_2("-O2"));

        if  (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("arm*"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("clang"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gcc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gdc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gfortran")) )
        {
            base.AddLinkerOption(wxT_2("-s"));
        }
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("avr*")) )
    {
        base.AddCompilerOption(wxT_2("-Os"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("dmc")) )
    {
        base.AddCompilerOption(wxT_2("-o"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("dmd"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("lcc")) )
    {
        base.AddCompilerOption(wxT_2("-O"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("icc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ifcwin")) )
    {
        base.AddCompilerOption(wxT_2("/O2"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ow")) )
    {
        base.AddCompilerOption(wxT_2("-ot")); // optimize for time
        base.AddCompilerOption(wxT_2("-ox")); // Maximum optimization
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("keilc51")))
    {
        base.AddCompilerOption(wxT_2("OPTIMIZE(11,SIZE)"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("keilcx51")))
    {
        base.AddCompilerOption(wxT_2("OPTIMIZE(11,SIZE)"));
        base.AddCompilerOption(wxT_2("OBJECTADVANCED"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("iar*")))
    {
        base.AddCompilerOption(wxT_2("-Oh"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("sdcc")) )
    {
        base.AddCompilerOption(wxT_2("--opt-code-size"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("null")) )
    {
        // the (pseudo) compiler "No Compiler" takes no options
    }
    else
    {
        ShowWarning(wxT_2("This wizard doesn't know how to setup optimization flags for this compiler.\n"));
    }
    // TODO: Other compilers, fallback?
}// OptimizationsOn

// C++ Exceptions On
function CppExceptionsOn(base, compilerID)
{
    if      (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("msvc*")))
    {
        base.AddCompilerOption(wxT_2("/EHsc"));
    }
    else if (   GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gcc"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("gfortran"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("clang"))
             || GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("cygwin")) )
    {
        base.AddCompilerOption(wxT_2("-fexceptions"));
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("bcc*")))
    {
        base.AddCompilerOption(wxT_2("-x"));
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("ow")))
    {
        base.AddCompilerOption(wxT_2("-xs"));
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("dmc")))
    {
        base.AddCompilerOption(wxT_2("-Ae"));
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("icc")))
    {
        base.AddCompilerOption(wxT_2("/EHs"));
    }
    else if (GetCompilerFactory().CompilerInheritsFrom(compilerID, wxT_2("null")))
    {
        // the (pseudo) compiler "No Compiler" takes no options
    }
    else
    {
        ShowWarning(wxT_2("This wizard doesn't know how to setup exception flags for this compiler.\n"));
    }
    // TODO: Other compilers, fallback?
}// CppExceptionsOn

// Verify a (provided) directory exists - otherwise throw an error.
// Return the true directory having replaced all possible macros.
function VerifyDirectory(dir_or_macro)
{
    // try to make it a real path and verify it's existence
    local dir = ReplaceMacros(dir_or_macro, true);

    if (!IO.DirectoryExists(dir))
    {
        ShowError(wxT_2("The directory you entered seems not to be valid.\n" +
                     "This wizard cannot continue."));
        return (wxT_2(""));
    }

    return dir;
}

function VerifyMacro(macro)
{
    // try to make it a real path and verify it's existence
    local dir = ReplaceMacros(macro, true);

    // verify if there are macros used at all...
    if (macro.Matches(dir))
    {
        return (wxT_2("")); // no macros used -> return empty
    }

    return dir;
}

// Get compiler include directory (taking GV's into account)
// selection        - the original directory selection the user has made
// defaultSelection - the default directory proposed by the wizard (using a macro)
// defaultSelection - the default include directory proposed by the wizard (using a macro)
// Returns: The include directory (which maybe translated from a macro)
function GetCompilerIncludeDir(selection, defaultSelection, defaultIncludeMacro)
{

    // make it a real path and verify it's existence
    local selection_nomacro = VerifyMacro(selection);

    // verify if there are macros used at all...
    if (selection_nomacro.IsEmpty())
    {
        // no macros used, direct path
        if (!IO.DirectoryExists(selection + wxFILE_SEP_PATH + wxT_2("include")))
        {
            ShowError(wxT_2("The path you entered seems valid, but the wizard " +
                         "can't locate the include directory.\n" +
                         "This wizard cannot continue."));
            return (wxT_2(""));
        }
        return (selection + wxFILE_SEP_PATH + wxT_2("include"));
    }

    // macros used

    if (selection.Matches(defaultSelection))
    {
        // default macro used

        // default include macro is usually: $(#GV.include)
        local defaultInclude_nomacro = ReplaceMacros(defaultIncludeMacro, true);
        if (!IO.DirectoryExists(defaultInclude_nomacro))
        {
            ShowError(wxT_2("The macro you entered seems valid, but this wizard can't\n" +
                         "locate the include directory based on this macro.\n" +
                         "This wizard cannot continue."));
            return (wxT_2(""));
        }

        // default include macro used
        return (defaultInclude_nomacro);
    }

    // non-default macro used

    if (!IO.DirectoryExists(selection_nomacro + wxFILE_SEP_PATH + wxT_2("include")))
    {
        ShowError(wxT_2("The macro you entered seems valid, but this wizard can't\n" +
                     "compute the include directory based on this macro.\n" +
                     "This wizard cannot continue."));
        return (wxT_2(""));
    }

    return (selection_nomacro + wxFILE_SEP_PATH + wxT_2("include"));
}

// Get compiler include macro (if possible and a GV has been provided)
// selection        - the original directory selection the user has made
// defaultSelection - the default directory proposed by the wizard (using a macro)
// defaultSelection - the default include directory proposed by the wizard (using a macro)
// Returns: The include macro (if possible), empty if not able to use a macro.
function GetCompilerIncludeMacro(selection, defaultSelection, defaultIncludeMacro)
{

    // make it a real path and verify it's existence
    local selection_nomacro = VerifyMacro(selection);

    // verify if there are macros used at all...
    if (!selection_nomacro.IsEmpty())
    {
        if (selection.Matches(defaultSelection))
        {
            // default macro used

            // default include macro is usually: $(#GV.include)
            local defaultInclude_nomacro = ReplaceMacros(defaultIncludeMacro, true);
            if (IO.DirectoryExists(defaultInclude_nomacro))
            {
                // default include macro used
                return (defaultInclude_nomacro);
            }
        }

        // non-default macro used

        if (IO.DirectoryExists(selection_nomacro + wxFILE_SEP_PATH + wxT_2("include")))
        {
            return (selection_nomacro + wxFILE_SEP_PATH + wxT_2("include"));
        }
    }

    // in all other cases no macro or is used or
    // the include macro cannot be computed
    return (wxT_2(""));
}

// Get compiler library directory (taking GV's into account)
// selection        - the original directory selection the user has made
// defaultSelection - the default directory proposed by the wizard (using a macro)
// defaultSelection - the default library directory proposed by the wizard (using a macro)
// Returns: The library directory (which maybe translated from a macro)
function GetCompilerLibDir(selection, defaultSelection, defaultLibMacro)
{

    // make it a real path and verify it's existence
    local selection_nomacro = VerifyMacro(selection);

    // verify if there are macros used at all...
    if (selection_nomacro.IsEmpty())
    {
        // no macros used, direct path
        if (!IO.DirectoryExists(selection + wxFILE_SEP_PATH + wxT_2("lib")))
        {
            ShowError(wxT_2("The path you entered seems valid, but the wizard " +
                         "can't locate the library directory.\n" +
                         "This wizard cannot continue."));
            return (wxT_2(""));
        }
        return (selection + wxFILE_SEP_PATH + wxT_2("lib"));
    }

    // macros used

    if (selection.Matches(defaultSelection))
    {
        // default macro used

        // default lib macro is usually: $(#GV.lib)
        local defaultInclude_nomacro = ReplaceMacros(defaultLibMacro, true);
        if (!IO.DirectoryExists(defaultInclude_nomacro))
        {
            ShowError(wxT_2("The macro you entered seems valid, but this wizard can't\n" +
                         "locate the library directory based on this macro.\n" +
                         "This wizard cannot continue."));
            return (wxT_2(""));
        }

        // default lib macro used
        return (defaultInclude_nomacro);
    }

    // non-default macro used

    if (!IO.DirectoryExists(selection_nomacro + wxFILE_SEP_PATH + wxT_2("lib")))
    {
        ShowError(wxT_2("The macro you entered seems valid, but this wizard can't\n" +
                     "compute the library directory based on this macro.\n" +
                     "This wizard cannot continue."));
        return (wxT_2(""));
    }

    return (selection_nomacro + wxFILE_SEP_PATH + wxT_2("lib"));
}

// Get compiler library macro (if possible and a GV has been provided)
// selection        - the original directory selection the user has made
// defaultSelection - the default directory proposed by the wizard (using a macro)
// defaultSelection - the default library directory proposed by the wizard (using a macro)
// Returns: The library macro (if possible), empty if not able to use a macro.
function GetCompilerLibMacro(selection, defaultSelection, defaultLibMacro)
{

    // make it a real path and verify it's existence
    local selection_nomacro = VerifyMacro(selection);

    // verify if there are macros used at all...
    if (!selection_nomacro.IsEmpty())
    {
        if (selection.Matches(defaultSelection))
        {
            // default macro used

            // default library macro is usually: $(#GV.lib)
            local defaultLib_nomacro = ReplaceMacros(defaultLibMacro, true);
            if (IO.DirectoryExists(defaultLib_nomacro))
            {
                // default library macro used
                return (defaultLib_nomacro);
            }
        }

        // non-default macro used

        if (IO.DirectoryExists(selection_nomacro + wxFILE_SEP_PATH + wxT_2("lib")))
        {
            return (selection_nomacro + wxFILE_SEP_PATH + wxT_2("lib"));
        }
    }

    // in all other cases no macro or is used or
    // the library macro cannot be computed
    return (wxT_2(""));
}

// Converting project's name to be valid c++ identifier
// (needed for class names) and valid c++ file name
// (i.e. can not contain unicode and forbidden chars)
function GetFixedProjectName(ProjectName)
{
    local Fixed = wxT_2("");
    for ( local i = 0; i < ProjectName.Length(); i++ )
    {
        local Char = ProjectName.GetChar(i);
        if  ( ( Char>='0' && Char<='9' && i > 0 ) ||
              ( Char>='a' && Char<='z' )          ||
              ( Char>='A' && Char<='Z' ) )
        {
            Fixed.AddChar(Char);
        }
        else
        {
            Fixed.AddChar('_');
        }
    }
    return Fixed;
}

// verify the existence of a file of specific type
// dir  = the directory the file is expected in
// file = name of the file to look for
// type = descriptive name of the file to show in the error message
// Returns: true, if the file exists, false otherwise
function VerifyFile(dir, file, type)
{
    if (!IO.FileExists(dir + wxFILE_SEP_PATH + file))
    {
        ShowError(wxT_2("The path you entered seems valid, but this wizard\n" +
                     "can't locate the following ") + type + wxT_2(" file:\n") +
                     file + wxT_2(" in it."));
        return false;
    }
    return true;
}

// verify the existence of a file of library type (add prefix lib, postfix .a and .lib)
// dir  = the directory the library is expected in
// file = name of the library to look for (usually for e.g. "libGL.a" providing "GL" is enough).
// Returns: true, if the library exists, false otherwise
function SilentVerifyLibFile(dir, file)
{
    return (   IO.FileExists(dir + wxFILE_SEP_PATH + file)
            || IO.FileExists(dir + wxFILE_SEP_PATH + file + wxT_2(".a"))
            || IO.FileExists(dir + wxFILE_SEP_PATH + file + wxT_2(".lib"))
            || IO.FileExists(dir + wxFILE_SEP_PATH + wxT_2("lib") + file)
            || IO.FileExists(dir + wxFILE_SEP_PATH + wxT_2("lib") + file + wxT_2(".a"))
            || IO.FileExists(dir + wxFILE_SEP_PATH + wxT_2("lib") + file + wxT_2(".lib"))
            || IO.FileExists(dir + wxFILE_SEP_PATH + wxT_2("lib") + file + wxT_2(".la"))
            || IO.FileExists(dir + wxFILE_SEP_PATH + wxT_2("lib") + file + wxT_2(".so")) );
}

// verify the existence of a file of library type (add prefix lib, postfix .a and .lib)
// dir  = the directory the library is expected in
// file = name of the library to look for (usually for e.g. "libGL.a" providing "GL" is enough).
// type = descriptive name of the library to show in the error message
// Returns: true, if the library exists, false otherwise
function VerifyLibFile(dir, file, type)
{
    if (!SilentVerifyLibFile(dir, file))
    {
        ShowError(wxT_2("The path you entered seems valid, but this wizard\n" +
                     "can't locate the following ") + type + wxT_2(" library file:\n") +
                     file + wxT_2(" in it.\n" +
                     "(Also tried prepending lib and appending .a and .lib)."));
        return false;
    }
    return true;
}

// Add a file to a selection of target(s).
// Thus the wizard must have had a FilePathPanel for the selection of these.
// the_wiz  = a reference to the wizard (to access the targets indexes)
// the_file = name of the file (including full path) that shall be added
function AddFileToTargets(the_wiz, the_file)
{
    // get the first selected target to add the file to
    local tgtidx = the_wiz.GetFileTargetIndex();
    if (tgtidx != -1)
    {
        // obtain the currently active project to add the file to
        local prj = GetProjectManager().GetActiveProject();
        if (!IsNull(prj))
        {
            // Check first if the file exists in Active Project
            local pf_check = prj.GetFileByFilename(the_file, false, false);
            if (!IsNull(pf_check)) // File exists, Remove it first
                prj.RemoveFile(pf_check)
            // Now add the file to the first selected target
            local pf = prj.AddFile(tgtidx, the_file);
            GetProjectManager().RebuildTree(); // make sure our view is current

            // if the file was added successfully, (...)
            if (!IsNull(pf))
            {
                // add to this file the rest of the selected targets...
                tgtidx = the_wiz.GetFileTargetIndex();
                while (tgtidx != -1)
                {
                    local tgt = prj.GetBuildTarget(tgtidx);
                    if (!IsNull(tgt))
                        pf.AddBuildTarget(tgt.GetTitle());

                    tgtidx = the_wiz.GetFileTargetIndex();
                }
            }
        }
    }
}
