////////////////////////////////////////////////////////////////////////////////
//
// SDL project wizard
//
////////////////////////////////////////////////////////////////////////////////

// globals (used under windows only)
SDLPathDefault    <- wxT_2("$(#sdl)");
SDLPathDefaultInc <- wxT_2("$(#sdl.include)");
SDLPathDefaultLib <- wxT_2("$(#sdl.lib)");
SDLPath           <- wxT_2("");

function BeginWizard()
{
    local intro_msg = wxT_2("Welcome to the new SDL project wizard!\n\n" +
                         "This wizard will guide you to create a new project\n" +
                         "using the SDL graphics library.\n\n" +
                         "When you 're ready to proceed, please click \"Next\"...");

    local sdlpath_descr = wxT_2("Please select the location of SDL on your computer.\n" +
                             "This is the top-level folder where SDL was installed (unpacked).\n" +
                             "To help you, this folder must contain the subfolders\n" +
                             "\"include\" and \"lib\".");

    Wizard.AddInfoPage(wxT_2("SDLIntro"), intro_msg);
    Wizard.AddProjectPathPage();
    if (PLATFORM == PLATFORM_MSW)
        Wizard.AddGenericSelectPathPage(wxT_2("SDLPath"), sdlpath_descr, wxT_2("Please select SDL's location:"), SDLPathDefault);
    Wizard.AddCompilerPage(wxT_2(""), wxT_2("*"), true, true);
}

////////////////////////////////////////////////////////////////////////////////
// SDL's path page
////////////////////////////////////////////////////////////////////////////////

function OnLeave_SDLPath(fwd)
{
    if (fwd)
    {
        local dir         = Wizard.GetTextControlValue(wxT_2("txtFolder")); // txtFolder is the text control in GenericSelectPathPage
        local dir_nomacro = VerifyDirectory(dir);

        if (dir_nomacro.IsEmpty())
            return false;

        // verify include dependencies
        local dir_nomacro_inc = GetCompilerIncludeDir(dir, SDLPathDefault, SDLPathDefaultInc);
        if (dir_nomacro_inc.IsEmpty())
            return false;

        if (!IO.FileExists(dir_nomacro_inc + wxFILE_SEP_PATH + wxT_2("SDL.h")))
        {
            if (VerifyFile(dir_nomacro_inc, wxT_2("SDL/SDL.h"), wxT_2("SDL's include")))
                SDLPathDefaultInc = SDLPathDefaultInc + wxT_2("/SDL");
            else
                return false;
        }

        // verify library dependencies
        local dir_nomacro_lib = GetCompilerLibDir(dir, SDLPathDefault, SDLPathDefaultLib);
        if (dir_nomacro_lib.IsEmpty())
            return false;

        if (!VerifyLibFile(dir_nomacro_lib, wxT_2("SDLmain"), wxT_2("SDL's")))
            return false;

        if (!VerifyLibFile(dir_nomacro_lib, wxT_2("SDL.dll"), wxT_2("SDL's")))
            return false;

        SDLPath = dir; // Remember the original selection.

        local is_macro = wxT_2("");

        // try to resolve the include directory as macro
        is_macro = GetCompilerIncludeMacro(dir, SDLPathDefault, SDLPathDefaultInc);
        if (is_macro.IsEmpty())
        {
            // not possible -> use the real inc path we had computed instead
            SDLPathDefaultInc = dir_nomacro_inc;
        }

        // try to resolve the library directory as macro
        is_macro = GetCompilerLibMacro(dir, SDLPathDefault, SDLPathDefaultLib);
        if (is_macro.IsEmpty())
        {
            // not possible -> use the real lib path we had computed instead
            SDLPathDefaultLib = dir_nomacro_lib;
        }
    }
    return true;
}

// return the files this project contains
function GetFilesDir()
{
    return wxT_2("sdl/files");
}

// setup the already created project
function SetupProject(project)
{
    if (PLATFORM == PLATFORM_MSW)
    {
        project.AddIncludeDir(SDLPathDefaultInc);
        project.AddLibDir(SDLPathDefaultLib);

        // add link libraries
        project.AddLinkLib(wxT_2("mingw32"));
        project.AddLinkLib(wxT_2("SDLmain"));
        project.AddLinkLib(wxT_2("SDL.dll"));
        project.AddLinkLib(wxT_2("user32"));
        project.AddLinkLib(wxT_2("gdi32"));
        project.AddLinkLib(wxT_2("winmm"));
        project.AddLinkLib(wxT_2("dxguid"));
    }
    else if (PLATFORM == PLATFORM_MAC)
    {
        //project.AddCompilerOption(wxT_2("-D_GNU_SOURCE=1 -D_THREAD_SAFE"));
        project.AddLinkerOption(wxT_2("-framework SDL"));
        // libSDLmain.a does not exist by default, needs to be built from SDLMain.m:
        project.AddLinkLib(wxT_2("SDLmain"));

        project.AddLinkerOption(wxT_2("-framework Cocoa")); // SDL dependency
    }
    else
    {
        // unix way
        project.AddCompilerOption(wxT_2("`sdl-config --cflags`"));
        project.AddLinkerOption(wxT_2("`sdl-config --libs`"));
    }

    // enable compiler warnings (project-wide)
    WarningsOn(project, Wizard.GetCompilerID());

    // Debug
    local target = project.GetBuildTarget(Wizard.GetDebugName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttConsoleOnly); // ttConsoleOnly: console for debugging
        target.SetOutputFilename(Wizard.GetDebugOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        // enable generation of debugging symbols for target
        DebugSymbolsOn(target, Wizard.GetCompilerID());
    }

    // Release
    target = project.GetBuildTarget(Wizard.GetReleaseName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttExecutable); // ttExecutable: no console
        target.SetOutputFilename(Wizard.GetReleaseOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        // enable optimizations for target
        OptimizationsOn(target, Wizard.GetCompilerID());
    }

    return true;
}
